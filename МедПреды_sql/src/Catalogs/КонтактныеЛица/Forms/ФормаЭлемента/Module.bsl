
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Ключ.Пустая() Тогда
		
		ПриСозданииПриЧтенииНаСервере();
		
		Если ЗначениеЗаполнено(КлассификаторДляЗаполненияКИ) Тогда
			ЗаполнитьКонтактнуюИнформацию(КлассификаторДляЗаполненияКИ);
		КонецЕсли;
		
	КонецЕсли;
	
	ЭтотОбъект.СобытиеДляПоказаПослеЗакрытия = Параметры.СобытиеДляПоказаПослеЗакрытия;
	
	Элементы.СпециализацияВрача.СписокВыбора.ЗагрузитьЗначения(ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("СпискиВыбора", "СпециализацииВрачаКонтактныхЛиц", Новый Массив));
	Элементы.СпециализацияВрача.КнопкаВыпадающегоСписка = Элементы.СпециализацияВрача.СписокВыбора.Количество() > 0;
		
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы И ЗначениеЗаполнено(СобытиеДляПоказаПослеЗакрытия) Тогда
		ПоказатьЗначение(, СобытиеДляПоказаПослеЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// УНФ.КонтактнаяИнформация
	КонтактнаяИнформацияМедПреды.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец УНФ.КонтактнаяИнформация
		
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Сохраним введеную специализация врача контактного лица в истории ввода
	Если Не ПустаяСтрока(Объект.СпециализацияВрача) Тогда
		
		// Удалим если значение уже было
		НайденныйЭлемент = Элементы.СпециализацияВрача.СписокВыбора.НайтиПоЗначению(СокрЛП(Объект.СпециализацияВрача));
		Если НайденныйЭлемент <> Неопределено Тогда
			Элементы.СпециализацияВрача.СписокВыбора.Удалить(НайденныйЭлемент);
		КонецЕсли;
		
		// Вставим на первое место
		Элементы.СпециализацияВрача.СписокВыбора.Вставить(0, СокрЛП(Объект.СпециализацияВрача));
		// Сократим список выбора до 10 значений
		Пока Элементы.СпециализацияВрача.СписокВыбора.Количество() > 10 Цикл
			Элементы.СпециализацияВрача.СписокВыбора.Удалить(Элементы.СпециализацияВрача.СписокВыбора.Количество()-1);
		КонецЦикла;
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("СпискиВыбора", "СпециализацииВрачаКонтактныхЛиц", Элементы.СпециализацияВрача.СписокВыбора.ВыгрузитьЗначения());
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрОповещения = Новый Структура;
	ПараметрОповещения.Вставить("КонтактноеЛицо",	Объект.Ссылка);
	ПараметрОповещения.Вставить("Владелец",			Объект.Владелец);
	ПараметрОповещения.Вставить("Наименование",		Объект.Наименование);
	
	Оповестить("Запись_КонтактноеЛицо", ПараметрОповещения, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// УНФ.КонтактнаяИнформация
	КонтактнаяИнформацияМедПреды.ОбработкаПроверкиЗаполненияНаСервере(ЭтотОбъект, Отказ);
	// Конец УНФ.КонтактнаяИнформация
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НаименованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	// УНФ.Google
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ОписаниеОповещения") Тогда
		СтандартнаяОбработка = Ложь;
		ВыполнитьОбработкуОповещения(ВыбранноеЗначение);
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторКонтактов") Тогда
		СтандартнаяОбработка = Ложь;
		ЗаполнитьКонтактнуюИнформацию(ВыбранноеЗначение);
		ОбновитьЭлементыКонтактнойИнформации();
	КонецЕсли;
	// Конец УНФ.Google
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	// УНФ.КонтактнаяИнформация
	КонтактнаяИнформацияМедПреды.ПриСозданииПриЧтенииНаСервере(ЭтотОбъект, , 6);
	// Конец УНФ.КонтактнаяИнформация
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыКонтактнойИнформации()
	
	// УНФ.КонтактнаяИнформация
	КонтактнаяИнформацияМедПреды.ОбновитьЭлементыКонтактнойИнформации(ЭтотОбъект);
	// Конец УНФ.КонтактнаяИнформация
	
КонецПроцедуры

#КонецОбласти

#Область КонтактнаяИнформацияМедПреды

&НаСервере
Процедура ДобавитьКонтактнуюИнформациюСервер(ДобавляемыйВид, УстановитьВыводВФормеВсегда = Ложь) Экспорт
	
	КонтактнаяИнформацияМедПреды.ДобавитьКонтактнуюИнформацию(ЭтотОбъект, ДобавляемыйВид, УстановитьВыводВФормеВсегда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДействиеКИНажатие(Элемент)
	
	КонтактнаяИнформацияМедПредыКлиент.ДействиеКИНажатие(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставлениеКИПриИзменении(Элемент)
	
	КонтактнаяИнформацияМедПредыКлиент.ПредставлениеКИПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставлениеКИНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	КонтактнаяИнформацияМедПредыКлиент.ПредставлениеКИНачалоВыбора(ЭтотОбъект, Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПредставлениеКИОчистка(Элемент, СтандартнаяОбработка)
	
	КонтактнаяИнформацияМедПредыКлиент.ПредставлениеКИОчистка(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КомментарийКИПриИзменении(Элемент)
	
	КонтактнаяИнформацияМедПредыКлиент.КомментарийКИПриИзменении(ЭтотОбъект, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КонтактнаяИнформацияУНФВыполнитьКоманду(Команда)
	
	КонтактнаяИнформацияМедПредыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область Google

&НаСервере
Процедура ЗаполнитьКонтактнуюИнформацию(Знач КлассификаторСсылка)
	
	ДанныеКонтакта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
	КлассификаторСсылка,
	"Title, JSON");
	
	Объект.Наименование = ДанныеКонтакта.Title;
	
	ЭтотОбъект.КонтактнаяИнформация.Очистить();
	
	Справочники.КлассификаторКонтактов.ЗаполнитьКонтактнуюИнформациюИзJSON(
	ЭтотОбъект.КонтактнаяИнформация,
	ДанныеКонтакта.JSON,
	Тип("СправочникСсылка.КонтактныеЛица"));
	
КонецПроцедуры

#КонецОбласти
