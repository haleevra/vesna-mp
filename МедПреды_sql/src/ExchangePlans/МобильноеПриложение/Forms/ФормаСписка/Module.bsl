
&НаКлиенте
Процедура Подключить(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораИзМеню", ЭтотОбъект);
	СписокПользователей = ПолучитьСписокПользователей();
	
	Если СписокПользователей.Количество() > 1 Тогда
		ПоказатьВыборИзМеню(Оповещение, СписокПользователей);
	Иначе
		ОткрытьФорму("ПланОбмена.МобильноеПриложение.Форма.ФормаПодключения");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораИзМеню(ВыбранныйЭлемент, Параметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено И ЗначениеЗаполнено(ВыбранныйЭлемент.Значение) Тогда
		ОткрытьФорму("ПланОбмена.МобильноеПриложение.Форма.ФормаПодключения", Новый Структура("Пользователь", ВыбранныйЭлемент.Значение));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПолучитьСписокПользователей()
	
	УстановитьПривилегированныйРежим(Истина);
	
	СписокПользователей = Новый СписокЗначений();
	
	
	Если РольДоступна("ПолныеПрава") Тогда
		ВыборкаПользователей = Справочники.Пользователи.Выбрать();
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Ссылка КАК Ссылка,
		|	ВложенныйЗапрос.Ссылка.Наименование КАК Наименование,
		|	ВложенныйЗапрос.Ссылка.Служебный КАК Служебный,
		|	ВложенныйЗапрос.Ссылка.Недействителен КАК Недействителен,
		|	ВложенныйЗапрос.Ссылка.ИдентификаторПользователяИБ КАК ИдентификаторПользователяИБ
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДоступныеПроекты.Супервайзер КАК Ссылка
		|	ИЗ
		|		РегистрСведений.ДоступныеПроекты КАК ДоступныеПроекты
		|	ГДЕ
		|		ДоступныеПроекты.Супервайзер = &Супервайзер
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДоступныеПроекты.МедПредставитель
		|	ИЗ
		|		РегистрСведений.ДоступныеПроекты КАК ДоступныеПроекты
		|	ГДЕ
		|		ДоступныеПроекты.Супервайзер = &Супервайзер) КАК ВложенныйЗапрос"
		;
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("Супервайзер", Пользователи.ТекущийПользователь());
		
		ВыборкаПользователей = Запрос.Выполнить().Выбрать();
	КонецЕсли;
	
	Пока ВыборкаПользователей.Следующий() Цикл
		Если ВыборкаПользователей.Наименование <> "<Не указан>"
			И НЕ ВыборкаПользователей.Служебный
			И НЕ ВыборкаПользователей.Недействителен Тогда
			
			СтруктураВозврата = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ВыборкаПользователей.ИдентификаторПользователяИБ);
			
			Если СтруктураВозврата <> Неопределено Тогда
				Логин = СтруктураВозврата.Имя;
				СписокПользователей.Добавить(Логин, Логин,,БиблиотекаКартинок.Пользователь);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СписокПользователей;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "НастройкаМобильногоПриложенияГотово" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	
КонецПроцедуры
